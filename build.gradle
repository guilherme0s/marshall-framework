plugins {
    id 'java'
    id 'jacoco'
    id 'checkstyle'
    id 'com.diffplug.spotless' version '8.0.0'
}

allprojects {
    group = 'com.marshal'
    version = '0.0.0'

    repositories {
        mavenCentral()
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'jacoco'
    apply plugin: 'com.diffplug.spotless'
    apply plugin: 'checkstyle'

    dependencies {
        implementation 'org.jspecify:jspecify:1.0.0'
        implementation 'net.kyori:adventure-api:4.24.0'
        testImplementation platform('org.junit:junit-bom:5.10.0')
        testImplementation 'org.junit.jupiter:junit-jupiter'
        testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
        testImplementation "org.mockito:mockito-core:5.20.0"
        testImplementation "org.mockito:mockito-junit-jupiter:5.20.0"
        testImplementation 'org.assertj:assertj-core:3.27.6'
    }

    test {
        useJUnitPlatform()
        finalizedBy jacocoTestReport
    }

    jacoco {
        toolVersion = "0.8.13"
    }

    jacocoTestReport {
        dependsOn test
        reports {
            xml.required = false
            csv.required = false
            html.outputLocation = layout.buildDirectory.dir('jacocoHtml')
        }
    }

    spotless {
        java {
            removeUnusedImports()
            importOrder('java', 'javax', 'net', 'org', 'com')
            endWithNewline()
        }
    }

    checkstyle {
        toolVersion = "10.12.4"
        configFile file('config/checkstyle/checkstyle.xml')
    }

    tasks.withType(Checkstyle).tap {
        configureEach {
            reports {
                xml.required = false
                html.required = true
            }
        }
    }
}
